apply plugin: 'java'
apply plugin: 'maven'

group = project.property('group')
version = project.property('version')

repositories {

    maven {
        name 'Spigot Repo'
        url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/'
    }

    // Custom repo for BungeeCord
    // That way, we can access the internal code, not just the API
    ivy {
        url 'https://ci.md-5.net/job/'
        layout 'pattern', {
            artifact '[artifact]/[revision]/artifact/bootstrap/target/[artifact].jar'
        }
    }

    jcenter()
}

dependencies {
    compile (name: 'BungeeCord', version: '1288') // Build number, version 1.12.2
    compile (group: 'org.spigotmc', name: 'spigot-api', version: '1.12.2-R0.1-SNAPSHOT')
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include '*.yml'
        // replace version and mcversion
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://localhost/tmp/repo/")
        }
    }
}